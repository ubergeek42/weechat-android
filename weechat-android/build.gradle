import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'org.aspectj:aspectjtools:1.8.6'
    }
}
apply plugin: 'com.android.application'

dependencies {
    repositories {
        google()
    }
    compile project (':cats')

    compile 'com.android.support:support-v4:27.0.2'
    compile 'com.android.support:support-annotations:27.0.2' // For @Nullable/@NonNull
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:preference-v7:27.0.2'  // preference fragment & al
    compile 'com.android.support:preference-v14:27.0.2' // styling for the fragment

    compile 'org.aspectj:aspectjrt:1.8.6'

    //compile "org.slf4j:slf4j-log4j12:1.7.12"  // Android Logging API
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.noveogroup.android:android-logger:1.3.5'

    compile "commons-codec:commons-codec:1.8"

    compile 'org.greenrobot:eventbus:3.0.0'

    // We need the weechat-relay library for connecting to weechat    
    compile(project(':weechat-relay')) {
        // Exclude this because slf4j-android is a superset of this
        exclude group: 'org.slf4j', module:'slf4j-api'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        versionCode 15
        versionName "0.12"

        minSdkVersion 16
        targetSdkVersion 27
        buildConfigField "String", "VERSION_BANNER", "\"" + versionBanner() + "\""
    }

    signingConfigs {
        releasePlayConfig
        devPlayConfig
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        // These are the real releases in the Google Play Store
        release {
            signingConfig signingConfigs.releasePlayConfig
            minifyEnabled true
            proguardFile file('proguard-rules.pro')
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile file('../cats/proguard-rules.pro')
        }

        // Development releases in the Google Play Store(signed same as the cloudbees site)
        devrelease {
            initWith release
            signingConfig signingConfigs.devPlayConfig
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
      abortOnError false
    }
}

// Load properties from the user's local gradle.properties file(for both release and "dev" release builds)
if (project.hasProperty('devStorefile') &&
    project.hasProperty('devStorePassword') &&
    project.hasProperty('devKeyAlias') &&
    project.hasProperty('devKeyPassword')) {
    android.signingConfigs.devPlayConfig.storeFile = file(devStorefile)
    android.signingConfigs.devPlayConfig.storePassword = devStorePassword
    android.signingConfigs.devPlayConfig.keyAlias = devKeyAlias
    android.signingConfigs.devPlayConfig.keyPassword = devKeyPassword
} else {
    project.logger.warn('WARN: Set the values devStorefile, devStorePassword, devKeyAlias, and devKeyPassword in ~/.gradle/gradle.properties to sign the release.')
    android.buildTypes.devrelease.signingConfig = null
}

if (project.hasProperty('releaseStorefile') &&
    project.hasProperty('releaseStorePassword') &&
    project.hasProperty('releaseKeyAlias') &&
    project.hasProperty('releaseKeyPassword')) {
    android.signingConfigs.releasePlayConfig.storeFile = file(releaseStorefile)
    android.signingConfigs.releasePlayConfig.storePassword = releaseStorePassword
    android.signingConfigs.releasePlayConfig.keyAlias = releaseKeyAlias
    android.signingConfigs.releasePlayConfig.keyPassword = releaseKeyPassword
} else {
    project.logger.warn('WARN: Set the values releaseStorefile, releaseStorePassword, releaseKeyAlias, and releaseKeyPassword in ~/.gradle/gradle.properties to sign the release.')
    android.buildTypes.release.signingConfig = null
}


static String versionBanner() {
    return 'git describe --long'.execute().text.trim()
}


tasks.withType(JavaCompile) {
    if (name != "compileDebugJavaWithJavac") return
    doLast {
        println "weaving cats into the app..."
        String[] args = [
                "-showWeaveInfo",
                "-1.5",
                "-inpath", destinationDir.toString(),
                "-aspectpath", classpath.asPath,
                "-d", destinationDir.toString(),
                "-classpath", classpath.asPath,
                "-bootclasspath", android.bootClasspath.join(File.pathSeparator)
        ]

        MessageHandler handler = new MessageHandler(true)
        new Main().run(args, handler)

        def log = project.logger
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error "ERROR: " + message.message, message.thrown
                    break
                case IMessage.WARNING:
                    log.warn "WARN: " + message.message, message.thrown
                    break
                case IMessage.INFO:
                    log.info "INFO: " + message.message, message.thrown
                    break
                case IMessage.DEBUG:
                    log.info "DEBUG " + message.message, message.thrown
                    break
            }
        }
    }
}
