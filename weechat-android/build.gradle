buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}
apply plugin: 'com.android.application'

dependencies {
    compile 'com.android.support:support-v4:22.1.+'
    compile 'com.android.support:support-annotations:+' // For @Nullable/@NonNull
    compile 'com.android.support:appcompat-v7:22.1.+'

    compile "org.slf4j:slf4j-android:1.7.12"  // Android Logging API

    compile "commons-codec:commons-codec:1.8"

    // We need the weechat-relay library for connecting to weechat    
    compile(project(':weechat-relay')) {
        // Exclude this because slf4j-android is a superset of this
        exclude group: 'org.slf4j', module:'slf4j-api'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    defaultConfig {
        versionCode 14
        versionName "0.10"

        minSdkVersion 8
        targetSdkVersion 21
    }

    signingConfigs {
	releasePlayConfig
	devPlayConfig
    }

    // Setup a bit of buildconfiguration
    buildTypes {
        // Debug version, only ever used locally
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "VERSION_BANNER", "\""+versionBanner()+"\""
        }

        // These are the real releases in the Google Play Store
        release {
            buildConfigField "String", "VERSION_BANNER", "\"" + versionBanner() + "\""
            signingConfig signingConfigs.releasePlayConfig

            minifyEnabled true
            proguardFile file('proguard.txt')
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
        }

        // Development releases in the Google Play Store(signed same as the cloudbees site)
        devrelease.initWith(buildTypes.release)
        devrelease {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField "String", "VERSION_BANNER", "\""+versionBanner()+"\""
	        signingConfig signingConfigs.devPlayConfig

            minifyEnabled true
            proguardFile file('proguard.txt')
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

// Load properties from the user's local gradle.properties file(for both release and "dev" release builds)
if (project.hasProperty('devStorefile') &&
    project.hasProperty('devStorePassword') &&
    project.hasProperty('devKeyAlias') &&
    project.hasProperty('devKeyPassword')) {
    android.signingConfigs.devPlayConfig.storeFile = file(devStorefile)
    android.signingConfigs.devPlayConfig.storePassword = devStorePassword
    android.signingConfigs.devPlayConfig.keyAlias = devKeyAlias
    android.signingConfigs.devPlayConfig.keyPassword = devKeyPassword
} else {
    project.logger.warn('WARN: Set the values devStorefile, devStorePassword, devKeyAlias, and devKeyPassword in ~/.gradle/gradle.properties to sign the release.')
    android.buildTypes.devrelease.signingConfig = null
}

if (project.hasProperty('releaseStorefile') &&
    project.hasProperty('releaseStorePassword') &&
    project.hasProperty('releaseKeyAlias') &&
    project.hasProperty('releaseKeyPassword')) {
    android.signingConfigs.releasePlayConfig.storeFile = file(releaseStorefile)
    android.signingConfigs.releasePlayConfig.storePassword = releaseStorePassword
    android.signingConfigs.releasePlayConfig.keyAlias = releaseKeyAlias
    android.signingConfigs.releasePlayConfig.keyPassword = releaseKeyPassword
} else {
    project.logger.warn('WARN: Set the values releaseStorefile, releaseStorePassword, releaseKeyAlias, and releaseKeyPassword in ~/.gradle/gradle.properties to sign the release.')
    android.buildTypes.release.signingConfig = null
}


String versionBanner() {
    return 'git describe'.execute().text.trim()
}



